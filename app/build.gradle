buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://oss.sonatype.org/content/groups/public' }
    maven { url "http://cloudant.github.io/cloudant-sync-eap/repository/" }
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    defaultConfig {
        applicationId "com.app.afridge"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 9
        versionName "2.1.0"

        // Enabling multidex support.
        multiDexEnabled true
        dexOptions {
            javaMaxHeapSize "4g"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = outputFile.name.replace(".apk", "${variant.versionName}-${variant.versionCode}.apk")
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
    android.applicationVariants.all { variant ->
        task("generate${variant.name.capitalize()}JavaDoc", type: Javadoc) {
            description "Generates Javadoc for $variant.name."
            source = variant.javaCompile.source
            destinationDir = new File("${project.getProjectDir()}/docs/compiled/", variant.baseName)
            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
            options.memberLevel = JavadocMemberLevel.PRIVATE
            options {
                links "http://docs.oracle.com/javase/7/docs/api/"
                linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
            }
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'me.relex:circleindicator:1.1.4@aar'
    compile 'com.etsy.android.grid:library:1.0.5'
    compile 'com.sababado.circularview:library:1.0.0'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.yalantis:contextmenu:1.0.4'
    compile 'com.github.navasmdc:MaterialDesign:1.5@aar'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.github.andkulikov:transitions-everywhere:1.3.2'
    compile 'com.github.asne:asne-facebook:0.3.3'
    compile 'com.github.asne:asne-twitter:0.3.3'
    compile 'com.github.asne:asne-googleplus:0.3.3'
    compile 'com.afollestad:material-dialogs:0.7.5.1'
    compile 'com.rengwuxian.materialedittext:library:2.1.3'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.eftimoff:androidplayer:1.0.2@aar'
    compile 'com.snappydb:snappydb-lib:0.5.0'
    compile 'com.kbeanie:image-chooser-library:1.4.4@aar'
    compile group: 'com.cloudant', name: 'cloudant-sync-datastore-core', version: '0.11.0'
    compile group: 'com.cloudant', name: 'cloudant-sync-datastore-android', version: '0.11.0'
    compile 'com.android.support:multidex:1.0.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.3.2@aar') {
        transitive = true;
    }
}
